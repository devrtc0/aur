# Maintainer: Ong Yong Xin <ongyongxin2020+github AT gmail DOT com>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=3.1.2
pkgrel=1
epoch=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=(i686 x86_64)
url="https://www.audacityteam.org/"
license=(GPL2 CCPL)
groups=(pro-audio)
depends=(
  alsa-lib
  expat
  flac
  gtk3
  libid3tag
  libmad
  libogg
  libsbsms
  libsndfile
  libsoxr
  libvorbis
  libx11
  lilv
  lv2
  portaudio
  portsmf
  soundtouch
  suil
  twolame
  vamp-plugin-sdk
  zlib
)
makedepends=(
  cmake
  conan
  ffmpeg
  gcc
  git
  gst-plugins-bad-libs
  gstreamer
  jack
  libnotify
  libsoup
  nasm
  sdl2
)
optdepends=('ffmpeg: additional import/export capabilities')
provides=(audacity)
conflicts=(audacity-git)
source=(
  "https://github.com/audacity/audacity/archive/refs/tags/Audacity-${pkgver}.tar.gz"
)
sha256sums=('4bd1175e613ddba2bebf98c244975b157d7998b5112de7f0a6dba8bd7b77214d')

build() {
  cd ${srcdir}/audacity-Audacity-${pkgver}
  mkdir build && cd build
  CC=gcc cmake \
    -G "Unix Makefiles" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Daudacity_obey_system_dependencies=On \
    -Daudacity_lib_preference=system \
    -Daudacity_use_wxwidgets=local \
    -Daudacity_use_ffmpeg=loaded \
    ..
  cmake --build .
}

package() {
  cd ${srcdir}/audacity-Audacity-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}
